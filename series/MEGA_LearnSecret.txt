%%
%% *:LearnSecretAboutNPC Content
%%   &CommonKnowledge = The secret you're looking for isn't much of a secret.
%%   &Incriminating   = The secret will incriminate E2
%%
%% The PC will have a chance to learn something about this particular NPC.
%% There's no guarantee that this will work.
%%
%% If this subplot is won, the following trigger must be set:
%%		.%id%_%plotid%_GoLearnSecret
%%
%% PARAM1: The Secret
%% PARAM2: The NPC
%% PARAM3: The city where this subplot is to happen
%%

Content
	name <Trivial Case>
	desc <Ask someone and get told the secret. How boring is that?>
	requires <*:LearnSecretAboutNPC ~&CommonKnowledge>
	Size 1

	% E1 is the secret
	% E2 is the NPC whom it involves
	% E3 is the city
	% E4 is a local public scene
	% E5 is a person who can tell the secret
	Element4 <Scene Public (Building|Meeting) !Near 3>
	Element5 <Character (Citizen|LABOR|ADVEN) !Near 4  NeverFail>
	NeverFail5 <Citizen>

	% FAIL CONDITIONS:
	% - E5 dies
	end <ifNPCDead %5% LoseSubPlot %plotid%>

	sub
		Persona 5
		rumor%id% <%name5% knows something about %name2%.>
		greeting <if= PlotStatus %plotid% %id% else %pop% Goto GoGreet>
		*GoGreet <*IWantToTalkAboutNPC %2% GoInform>
		GoInform <NewChat Say 1 Trigger0 .%id%_%plotid%_GoLearnSecret  WinSubPlot %plotid%>
		Msg1 <I don't know much, but I've heard rumors. \SECRET %1%>
	end


Content
	name <Buy A Secret>
	desc <You can buy a secret from a reporter.>
	requires <*:LearnSecretAboutNPC>
	Size 2

	% E1 is the secret
	% E2 is the NPC whom it involves
	% E3 is the city
	% E4 is a local public scene
	% E5 is a person who can tell the secret
	Element4 <Scene Public (Building|Meeting) !Near 3>
	Element5 <Character Sociable (MEDIA|NoFac) !Near 4 NeverFail>
	NeverFail5 <Journalist>

	% FAIL CONDITIONS:
	% - E5 dies
	start <ifNPCDead %5% LoseSubPlot %plotid%>

	sub
		Persona 5
		rumor%id% <%name5% can tell you something about %name2%.>
		% V1 = Information Cost
		% V2 = Got the Discount
		greeting <if= PlotStatus %plotid% %id% else GoChat Goto GoGreet>
		GoChat <NewChat SayAnything>
		*GoGreet <*IHaveInformation&AboutNPC GoCalc %2%>
		GoCalc <if= v1 0 else GoInform v= 1 Price %threat% 50 Goto GoInform>
		GoInform <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3 AddChat 4>
		GoFFFail <V= 1 Price StoryDL 25 V= 2 1 NewChat Say 3 AddChat 1 AddChat 2>
		result1 <NewChat Say 2 Cash+ -v1 Trigger0 .%id%_%plotid%_GoLearnSecret  WinSubPlot %plotid%>
		*result2 <*HurryBackWithMoney>
		result3 <ifConversation SkillTar %threat% else GoFFFail NewChat Say 4  Trigger0 .%id%_%plotid%_GoLearnSecret  WinSubPlot %plotid%>
		*result4 <*YourLossSucker GoCancelSubplot>
		GoCancelSubplot <LoseSubPlot %plotid%>
		Msg1 <I've got some useful information for you... Of course, I have to cover my own expenses. You can have it for $ \VAL V1 .>
		Msg2 <It's been a pleasure doing business with you. \SECRET %1%>
		Msg3 <Well, I suppose I can give you a discount... You can have the information for $ \VAL V1 . Isn't that a bargain?>
		Msg4 <I guess this could be pretty important to you... fine, I'll tell you. \SECRET %1%>
		Prompt1 <Alright, give me the info. [Pay $ \VAL V1 ]>
		CPrompt1 <ifG PC$ v1 Accept>
		Prompt2 <I'll be back for it some other time.>
		Prompt3 <Come on, this is \PC you're talking to!>
		CPrompt3 <ifG ChatNPCRelationship 0 Accept>
		Prompt4 <I'm not interested.>
	end



